PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX event: <http://purl.org/NET/c4dm/event.owl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>

select ?type ?title ?contrib ?dcoId ?year ?doi ?journal ?volume ?issue ?pagestart ?pageend (GROUP_CONCAT(distinct ?inner_label; separator=';') as ?Authors) (GROUP_CONCAT(distinct ?comms_l; separator=';') as ?Communities) (GROUP_CONCAT(distinct ?teams_l; separator=';') as ?Teams) where
{
  <URI> vitro:mostSpecificType ?t .
  ?t rdfs:label ?type .
  <URI> rdfs:label ?title .
    { SELECT ?inner_rank ?inner_label ?comms_l WHERE {
    <URI> vivo:relatedBy ?something .
    ?something a vivo:Authorship .
    OPTIONAL { ?something vivo:rank ?inner_rank . }
    OPTIONAL { <URI> dco:associatedDCOCommunity ?comms . ?comms rdfs:label ?comms_l . }
    ?something vivo:relates ?person .
    ?person a foaf:Person .
    ?person rdfs:label ?inner_label .
  } ORDER BY xsd:int(?inner_rank) }
  OPTIONAL { <URI> dco:isContributionToDCO ?contrib . }
  OPTIONAL { <URI> vivo:hasPublicationVenue/rdfs:label ?journal }
  OPTIONAL { { <URI> dco:yearOfPublication ?year } }
  OPTIONAL { <URI> dco:associatedDCOCommunity ?comms . ?comms rdfs:label ?comms_l . }
  OPTIONAL { <URI> dco:associatedDCOTeam ?teams . ?teams rdfs:label ?teams_l . }
  OPTIONAL { <URI> bibo:doi ?doi }
  OPTIONAL { <URI> dco:hasDcoId ?dcoId }
  OPTIONAL { <URI> bibo:pageStart ?pagestart . <URI> bibo:pageEnd ?pageend . }
  OPTIONAL { <URI> bibo:volume ?volume }
  OPTIONAL { <URI> bibo:issue ?issue }
} GROUP BY ?type ?title ?contrib ?doi ?journal ?year ?doi ?dcoId ?volume ?issue ?pagestart ?pageend
